apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.chenyoca'
version = '0.1-SNAPSHOT'

ext.UsingSonatype = hasProperty('UsingSonatype')
ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")
ext.ReleaseUrl = UsingSonatype ? SONATYPE_RELEASE_URL : RELEASE_URL
ext.SnapshotUrl = UsingSonatype ? SONATYPE_SNAPSHOT_URL : SNAPSHOT_URL
ext.Username = UsingSonatype ? SONATYPE_NEXUS_USERNAME : NEXUS_USERNAME
ext.Password = UsingSonatype ? SONATYPE_NEXUS_PASSWORD : NEXUS_PASSWORD

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = 'com.github.chenyoca'
                pom.artifactId = 'snippet'
                pom.version = version

                repository(url: ReleaseUrl) {
                    authentication(userName: Username, password: Password)
                }
                snapshotRepository(url: SnapshotUrl) {
                    authentication(userName: Username, password: Password)
                }

                pom.project {
                    name 'AndroidSnippet'
                    packaging 'jar'
                    description 'Most used code snippet at my work.'
                    url 'https://github.com/chenyoca/android-snippet'

                    scm {
                        url 'https://github.com/chenyoca/android-snippet'
                        connection 'scm:git@github.com:chenyoca/android-snippet.git'
                        developerConnection 'scm:git@github.com:chenyoca/android-snippet.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'chenyoca'
                            name 'YooJia Chen'
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }
}
